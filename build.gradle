plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'war'
}

group = 'com.lazyprog'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

sourceSets {
    main {
        java {
            srcDir 'lib'
        }
        resources {
            srcDir 'assets'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

buildDir = '_build'

configurations {
    all*.exclude module : 'spring-boot-starter-logging'
}

dependencies {
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.12'
    implementation 'org.modelmapper:modelmapper:2.4.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2:2.4.5'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    runtimeOnly 'mysql:mysql-connector-java'
}

test {
    useJUnitPlatform()
}

task deleteDeps(type: Delete) {
    delete 'lib/com/lazyprog/Version.java'
}

task versionSet(type: Copy, dependsOn: deleteDeps) { 
    from 'Version.java'
    into 'lib/com/lazyprog'
    expand(version: version)
}

compileJava.dependsOn(['versionSet'])
